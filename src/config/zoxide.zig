const std = @import("std");
const builtin = @import("builtin");
const consts = @import("consts");
const compress = @import("compress");

const common = @import("./common.zig");

const logger = std.log.scoped(.zoxide);

const GITHUB_API_URL = "https://api.github.com/repos/ajeetdsouza/zoxide/releases/latest";

pub const interface: common.ConfInterface = .{
    .name = "zoxide",
    .type = .Package,
    .manPages = &.{
        "man/man1/zoxide-add.1",
        "man/man1/zoxide-import.1",
        "man/man1/zoxide-init.1",
        "man/man1/zoxide-query.1",
        "man/man1/zoxide-remove.1",
        "man/man1/zoxide.1",
    },
    .getDownloadTargets = common.FetchRelease(.{
        .logger = logger,
        .relaseUrl = GITHUB_API_URL,
        .matchingAsset = matchingAsset,
        .toSemverString = common.stripV,
    }),
    .decompressTargetFile = common.DecompressExeName("zoxide"),
};

fn matchingAsset(name: []const u8) bool {
    const targetFilename = comptime getTargetFilename();

    return std.mem.endsWith(u8, name, targetFilename orelse return false);
}

fn getTargetFilename() ?[]const u8 {
    const arch = switch (builtin.target.cpu.arch) {
        .x86_64 => "x86_64",
        .aarch64 => "aarch64",
        .x86 => "i686",
        .arm => "armv7",
        else => return null,
    };

    const os_spec = switch (builtin.target.os.tag) {
        .linux => "unknown-linux-gnu",
        .macos => "apple-darwin",
        .windows => "pc-windows-msvc",
        else => return null,
    };

    const extension = if (builtin.target.os.tag == .windows) "zip" else "tar.gz";

    return std.fmt.comptimePrint("{s}-{s}.{s}", .{ arch, os_spec, extension });
}
